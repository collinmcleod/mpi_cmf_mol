!
! Routine to compute the opacity & emissivity variation matrices for
! the case with lines. Also computes the matrix dRHS_dCHI which
! multiply's %KI in the V equation. X is the line profile. It is assumed
! that :-
!				WRK_MAT( , ,1)=dCHI
!				WRK_MAT( , ,2)=dETA
!  				dRHS_dHdCHI( , ,)  !RHS of H equation.
!
	SUBROUTINE EDD_J_HUB_VAR_MPI_V1(dTAUdCHI,
	1                  SOURCE,CHI,ESEC,ES_COH_VEC,DTAU,R,
	1                  EDDF,Q,HU,HL,HS,HT,RSQ_DTAUONQ,DERIV_SCL_FAC,
	1                  W,WPREV,PSI,PSIPREV,DJDt,DJDt_OLDt,
	1                  JNU,JNUM1,JNU_OLDt,JNU_MOD,
	1                  RSQ_HNUM1,RSQ_HNU_OLDt,
	1                  dRHSdCHI_IB,dRHSdCHI_OB,
	1                  JMIN_IB,KMIN_IB,JPLUS_IB,KPLUS_IB,
	1                  JMIN_OB,KMIN_OB,JPLUS_OB,KPLUS_OB,
	1                  INNER_BND_METH,OUTER_BND_METH,
	1                  DST,DEND,ND,NM)
	USE SET_KIND_MODULE
	USE MOD_VAR_OPAC_J, ONLY : KI, RHS_dHdCHI, VDST, VDEND
	USE MPI
	IMPLICIT NONE
!
! Altered: 24-Apr-2019 -- Added DERIV_SCL_FAC to call.
!                         Changed to V3
! Created: 18-Jan-2010 -- Based on EDD_H_HUB_VAR_V1; Modified call.
!                         INNER_BND_METH & OUTER_BND_METH options installed.
!
	INTEGER DST,DEND
	INTEGER ND,NM
	REAL(KIND=LDP) dTAUdCHI(ND,ND)
	REAL(KIND=LDP) SOURCE(ND)
	REAL(KIND=LDP) CHI(ND)
	REAL(KIND=LDP) ESEC(ND)
	REAL(KIND=LDP) ES_COH_VEC(ND)
	REAL(KIND=LDP) DTAU(ND)
	REAL(KIND=LDP) R(ND)
	REAL(KIND=LDP) EDDF(ND)
	REAL(KIND=LDP) Q(ND)
!
	REAL(KIND=LDP) HU(ND),HL(ND),HS(ND),HT(ND)
	REAL(KIND=LDP) RSQ_DTAUONQ(ND)
	REAL(KIND=LDP) DERIV_SCL_FAC(ND)
	REAL(KIND=LDP) W(ND),WPREV(ND)
	REAL(KIND=LDP) DJDt(ND),DJDt_OLDt(ND)
	REAL(KIND=LDP) PSI(ND),PSIPREV(ND)
!
	REAL(KIND=LDP) JNU(ND)
	REAL(KIND=LDP) JNUM1(ND)
	REAL(KIND=LDP) JNU_OLDt(ND)
	REAL(KIND=LDP) JNU_MOD(ND)
	REAL(KIND=LDP) RSQ_HNUM1(ND),RSQ_HNU_OLDt(ND)
	REAL(KIND=LDP) dRHSdCHI_IB
	REAL(KIND=LDP) dRHSdCHI_OB
	REAL(KIND=LDP) JMIN_IB,KMIN_IB,JPLUS_IB,KPLUS_IB
	REAL(KIND=LDP) JMIN_OB,KMIN_OB,JPLUS_OB,KPLUS_OB
!
	INTEGER ERROR_LU
	EXTERNAL ERROR_LU
	CHARACTER(LEN=*) INNER_BND_METH
	CHARACTER(LEN=*) OUTER_BND_METH
!
!
! Local vectors.
!
	REAL(KIND=LDP) dHUdCHI(ND),dHLdCHI(ND)
	REAL(KIND=LDP) dHSdCHI(ND),dHTdCHI(ND)
	REAL(KIND=LDP) dHUdTAU(ND),dHLdTAU(ND)
	REAL(KIND=LDP) dRHSdI(ND),dRHSdJ(ND)
!
! Local varoables.
!
	REAL(KIND=LDP) WRK_MAT(ND,ND)
!
	REAL(KIND=LDP) T1
	REAL(KIND=LDP) MOD_DTAU
	REAL(KIND=LDP) dUdCHI
	REAL(KIND=LDP) dTAdCHI_J,dTAdCHI_I
	REAL(KIND=LDP) dTCdCHI_I,dTCdCHI_K
	REAL(KIND=LDP) dTBdCHI_J,dTBdCHI_I,dTBdCHI_K,dTBdCHI
!
	INTEGER I,J,K,L
	INTEGER MDST,MDEND,IERR
!
! 
!
	IF(NM .LT. 2)THEN
	  I=ERROR_LU()
	  WRITE(I,*)'Error in EDD_J_VAR_V4 - NM_KI too small'
	  WRITE(I,*)'NM_KI='
	  STOP
	END IF
	WRK_MAT=0.0_LDP
	RHS_dHdCHI=0.0_LDP
	MDST=MAX(1,DST-1)
	MDEND=MAX(DEND+1,ND)
!
! Compute the dTAUdCHI matrix.
!
	CALL dSPHEREdCHI(dTAUdCHI,DTAU,R,Q,ND)
!
! The following derivatives are valid for all ML.
!
	DO I=MDST,MIN(MDEND,ND-1)
	  T1=(1.0_LDP+W(I))*(CHI(I)+CHI(I+1))
	  dHUdCHI(I)=HU(I)*W(I)/T1
	  dHUdTAU(I)=-HU(I)/DTAU(I)
	  dHLdCHI(I)=HL(I)*W(I)/T1
	  dHLdTAU(I)=-HL(I)/DTAU(I)
	  dHSdCHI(I)=-HS(I)/T1
	  dHTdCHI(I)=-HT(I)/T1
	END DO
!
! 
!
! Firstly we compute the variation of the elements with respect to
! DTAU. We then multiply by dTAUdCHI matrix.
!
! We have 3 separate loops over I to allow vectorization.
! To improve rounding error, we note that dTBdCHI needs to be added to
! both dTBdCHI_I and dTBdCHI_J.
!
	DO I=MAX(DST,2),MIN(DEND,ND-1)
	  J=I-1
	  K=I+1
	  dTAdCHI_J=-dHLdTAU(J)
	  dTCdCHI_I=-dHUdTAU(I)
	  dTBdCHI_I=dHLdTAU(I)
	  dTBdCHI_J=dHUdTAU(J)
	  T1=0.5_LDP*R(I)*R(I)/Q(I)
	  dTBdCHI=(PSI(I)+DJDT(I))/(DTAU(J)+DTAU(I))+T1*(1.0_LDP-ES_COH_VEC(I))
!
	  dUdCHI=(PSIPREV(I)*JNUM1(I)+DJDT_OLDt(I)*JNU_OLDt(I))/(DTAU(J)+DTAU(I))
	  dRHSdJ(I)=  T1*SOURCE(I)
	1         - (dTAdCHI_J*JNU(J)+dTBdCHI_J*JNU(I))
	1         - dTBdCHI*JNU(I)
	1         + dUdCHI
!
	  dRHSdI(I)= T1*SOURCE(I)
	1         - (dTCdCHI_I*JNU(K)+dTBdCHI_I*JNU(I))
	1         + dUdCHI-dTBdCHI*JNU(I)
!
	END DO
!
	DO I=MAX(DST,2),MIN(DEND,ND-1)
	  J=I-1
	  K=I+1
	  DO L=1,ND
	    WRK_MAT(I,L)=WRK_MAT(I,L)+dRHSdJ(I)*dTAUdCHI(J,L)
	    WRK_MAT(I,L)=WRK_MAT(I,L)+dRHSdI(I)*dTAUdCHI(I,L)
	  END DO
	END DO
!
! Can now update WRK_MAT for direct opacity variation.
!
	DO I=MAX(DST,2),MIN(DEND,ND-1)
	  J=I-1
	  K=I+1
	  T1=0.5_LDP*R(I)*R(I)/Q(I)
!
	  dTAdCHI_J=-dHLdCHI(J)
	  dTAdCHI_I=-dHLdCHI(J)
	  dTCdCHI_I=-dHUdCHI(I)
	  dTCdCHI_K=-dHUdCHI(I)
!
	  dTBdCHI_J=dHUdCHI(J)
	  dTBdCHI_I=dHLdCHI(I)+dHUdCHI(J)
	  dTBdCHI= RSQ_DTAUONQ(I)*ES_COH_VEC(I)/CHI(I)-(PSI(I)+DJDT(I))/CHI(I)
	  dTBdCHI_K=dHLdCHI(I)
!
! Note: We have changed sign of dUdCHI reative to an earlier version.
!
	  dUdCHI=(PSIPREV(I)*JNUM1(I)+DJDT_OLDt(I)*JNU_OLDt(I))/CHI(I)
!
! NB  :  VB(I)=-HS(J) and VC(I)=HS(I)
!
	  WRK_MAT(I,J)=WRK_MAT(I,J)
	1             - (dTAdCHI_J*JNU(J)+dTBdCHI_J*JNU(I))
	1             - DERIV_SCL_FAC(I)*dHSdCHI(J)*RSQ_HNUM1(J)
	1             - dHTdCHI(J)*RSQ_HNU_OLDt(J)
!
	  WRK_MAT(I,K)=WRK_MAT(I,K)
	1             - (dTCdCHI_K*JNU(K)+dTBdCHI_K*JNU(I))
	1             + DERIV_SCL_FAC(I)*dHSDCHI(I)*RSQ_HNUM1(I)
	1             + dHTdCHI(I)*RSQ_HNU_OLDt(I)
!
	  T1=T1*(DTAU(J)+DTAU(I))/CHI(I)
	  WRK_MAT(I,I)=WRK_MAT(I,I)
	1             - (dTAdCHI_I*JNU(J)+dTCdCHI_I*JNU(K)+dTBdCHI_I*JNU(I))
	1             - (dUdCHI+dTBdCHI*JNU(I))
	1             + DERIV_SCL_FAC(I)*(dHSDCHI(I)*RSQ_HNUM1(I)-dHSDCHI(J)*RSQ_HNUM1(J))
	1             + (dHTDCHI(I)*RSQ_HNU_OLDt(I)-dHTDCHI(J)*RSQ_HNU_OLDt(J))
	1             - T1*SOURCE(I)
!
	  KI(I,I,2)=T1
	END DO
!
! Now do the boundary conditions.
!
	IF(DST .EQ. 1)THEN
	  IF(OUTER_BND_METH .EQ. 'HONJ')THEN
	    T1=  ( EDDF(1)*Q(1)*JNU(1)*R(1)*R(1) - EDDF(2)*Q(2)*JNU(2)*R(2)*R(2) )/DTAU(1)/DTAU(1)
	    DO L=1,ND
	      WRK_MAT(1,L)=WRK_MAT(1,L)+T1*dTAUdCHI(1,L)
	    END DO
	    WRK_MAT(1,1)=WRK_MAT(1,1) + dRHSdCHI_OB +
	1                ( PSI(1)*JNU(1)- PSIPREV(1)*JNUM1(1) )/CHI(1) +
	1                ( DJDT(1)*JNU(1)- DJDt_OLDt(1)*JNU_OLDt(1) )/CHI(1)
	ELSE IF(OUTER_BND_METH .EQ. 'HALF_MOM')THEN
	    MOD_DTAU=0.5_LDP*(CHI(1)-CHI(2))*(R(1)-R(2))
	    T1= ( KPLUS_OB*JNU_MOD(1)*R(1)*R(1)/JPLUS_OB - EDDF(2)*JNU(2)*R(2)*R(2) + KMIN_OB*R(1)*R(1))/MOD_DTAU/MOD_DTAU
	    WRK_MAT(1,1)=T1*0.5_LDP*(R(1)-R(2))
	    WRK_MAT(1,2)=T1*0.5_LDP*(R(1)-R(2))
	    WRK_MAT(1,1)=WRK_MAT(1,1) + dRHSdCHI_OB +
	1               ( PSI(1)*JNU_MOD(1)- PSIPREV(1)*JNUM1(1) )/CHI(1) +
	1               ( DJDT(1)*JNU_MOD(1)- DJDt_OLDt(1)*JNU_OLDt(1) )/CHI(1)
	    WRK_MAT(2,1)=WRK_MAT(2,1) + JMIN_OB* dHLdTAU(1)*dTAUdCHI(1,3)
	    WRK_MAT(2,2)=WRK_MAT(2,2) + JMIN_OB*(dHLdTAU(1)*dTAUdCHI(1,2) + dHLdCHI(1))
	    WRK_MAT(2,1)=WRK_MAT(2,1) + JMIN_OB* dHLdTAU(1)*dTAUdCHI(1,1)
	  ELSE
	    I=ERROR_LU()
	    WRITE(I,*)'Only outer boundary conditions implemented are HONJ & HALF_MOMJ'
	    WRITE(I,*)'Error occured in EDD_J_HUB_VAR_V2'
	    WRITE(I,*)'OUTER_BND_METH=',TRIM(OUTER_BND_METH)
	    STOP
	  END IF
	END IF
!
! Inner boundary --- diffusion approximation.
!
	IF(DEND .EQ. ND)THEN
	  IF(INNER_BND_METH .EQ. 'DIFFUSION')THEN
	    T1= ( R(ND)*R(ND)*EDDF(ND)*JNU(ND) - R(ND-1)*R(ND-1)*EDDF(ND-1)*Q(ND-1)*JNU(ND-1) )
	1           /DTAU(ND-1)/DTAU(ND-1)
	    DO L=ND-5,ND
	      WRK_MAT(ND,L)=WRK_MAT(ND,L)+T1*dTAUdCHI(ND-1,L)
	    END DO
	    WRK_MAT(ND,ND)=WRK_MAT(ND,ND) + dRHSdCHI_IB +
	1                 ( PSI(ND)*JNU(ND)- PSIPREV(ND)*JNUM1(ND) )/CHI(ND) +
	1                 ( DJDT(ND)*JNU(ND)- DJDt_OLDt(ND)*JNU_OLDt(ND) )/CHI(ND)
	  ELSE IF(INNER_BND_METH .EQ. 'ZERO_FLUX')THEN
	    T1= ( R(ND)*R(ND)*EDDF(ND)*JNU(ND) - R(ND-1)*R(ND-1)*EDDF(ND-1)*Q(ND-1)*JNU(ND-1) )
	1             /DTAU(ND-1)/DTAU(ND-1)
	    DO L=ND-5,ND
	      WRK_MAT(ND,L)=WRK_MAT(ND,L)+T1*dTAUdCHI(ND-1,L)
	    END DO
	    WRK_MAT(ND,ND)=WRK_MAT(ND,ND) + dRHSdCHI_IB +
	1                 ( PSI(ND)*JNU(ND)- PSIPREV(ND)*JNUM1(ND) )/CHI(ND) +
	1                 ( DJDT(ND)*JNU(ND)- DJDt_OLDt(ND)*JNU_OLDt(ND) )/CHI(ND)
	  ELSE IF(INNER_BND_METH .EQ. 'HOLLOW')THEN
	    T1= ( R(ND)*R(ND)*(JNU_MOD(ND)*(KMIN_IB/JMIN_IB)+KPLUS_IB) -
	1            R(ND-1)*R(ND-1)*EDDF(ND-1)*JNU(ND-1)  )
	1            /DTAU(ND-1)/DTAU(ND-1)
	    DO L=ND-5,ND
	      WRK_MAT(ND,L)=WRK_MAT(ND,L)+T1*dTAUdCHI(ND-1,L)
	    END DO
	    WRK_MAT(ND,ND)=WRK_MAT(ND,ND) + dRHSdCHI_IB +
	1                 ( PSI(ND)*JNU_MOD(ND)- PSIPREV(ND)*JNUM1(ND) )/CHI(ND) +
	1                 ( DJDT(ND)*JNU_MOD(ND)- DJDt_OLDt(ND)*JNU_OLDt(ND) )/CHI(ND)
	    WRK_MAT(ND-1,ND-2)=WRK_MAT(ND-1,ND-2) + JPLUS_IB* dHUdTAU(ND-1)*dTAUdCHI(ND-1,ND-2)
	    WRK_MAT(ND-1,ND-1)=WRK_MAT(ND-1,ND-1) + JPLUS_IB*(dHUdTAU(ND-1)*dTAUdCHI(ND-1,ND-1) + dHUdCHI(ND-1))
	    WRK_MAT(ND-1,ND)  =WRK_MAT(ND-1,ND)   + JPLUS_IB* dHUdTAU(ND-1)*dTAUdCHI(ND-1,ND)
	  ELSE
	    I=ERROR_LU()
	    WRITE(I,*)'Only boundary conditions implemented are DIFFUSION, ZERO_FLUX, & HOLLOW'
	    WRITE(I,*)'Error occured in EDD_J_HUB_VAR_V2'
	    WRITE(I,*)'INNER_BND_METH=',TRIM(INNER_BND_METH)
	    STOP
	  END IF
	END IF
!
	I=ND*ND
	CALL MPI_ALLREDUCE(MPI_IN_PLACE,WRK_MAT,I,MPI_DOUBLE_PRECISION,MPI_SUM,MPI_COMM_WORLD,IERR)
	DO J=VDST,VDEND
	  KI(:,J,1)=WRK_MAT(:,J)
	END DO
!
! 
!
! We compute the variation of the equation which updates the
! flux variation. We don't correct for the line profile, since
! we would the require two matrices. Note that HU(I), HL(I),
! HS(I), and HT(I) depend directly on CHI(I) and CHI(I+1).
!
	DO I=1,ND-1
	  T1=dHUdTAU(I)*JNU(I+1)-dHLdTAU(I)*JNU(I)
	  DO L=DST,MIN(DST,DST+1)
	    RHS_dHdCHI(I,L)=RHS_dHdCHI(I,L)+T1*dTAUdCHI(I,L)
	  END DO
	  T1=dHUdCHI(I)*JNU(I+1) - dHLdCHI(I)*JNU(I)
	1                       + dHSdCHI(I)*RSQ_HNUM1(I)
	1                       + dHTdCHI(I)*RSQ_HNU_OLDt(I)
	  RHS_dHdCHI(I,I)=RHS_dHdCHI(I,I) + T1
	  IF(I .LT. ND .AND. I .LE. DEND)RHS_dHdCHI(I,I+1)=RHS_dHdCHI(I,I+1) + T1
	END DO
!
	RETURN
	END
